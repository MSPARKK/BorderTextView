ext {
    PUBLISH_GROUP_ID = 'io.github.msparkk'
    PUBLISH_VERSION = '0.0.4'
    PUBLISH_ARTIFACT_ID = 'BorderTextView'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourceJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}
artifacts {
    archives androidSourceJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourceJar

                pom {
                    name = 'BorderTextView'
                    description = 'TextView with Border'
                    url = 'https://github.com/MSPARKK/BorderTextView'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/MSPARKK/BorderTextView/blob/main/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'MSPARK'
                            name = 'MSPARK'
                            email = 'pms93009@gmail.com'
                            url = 'https://github.com/MSPARKK'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/MSPARKK/BorderTextView.git'
                        developerConnection = 'scm:git:ssh://github.com:MSPARKK/BorderTextView.git'
                        url = 'https://github.com/MSPARKK/BorderTextView/tree/feature_maven_central'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}